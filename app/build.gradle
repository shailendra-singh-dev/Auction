buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 1
    def versionBuild = 0

    defaultConfig {
        applicationId "com.shail.auctionapp"
        minSdkVersion 17
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild

        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            keyAlias 'keyAlias'
            keyPassword 'keyPassword'
            storeFile file('/Users/peter/storeFile.jks')
            storePassword ".Android"
        }
    }

    buildTypes {
        debug {
            //Debug enabling..
            debuggable true

            //Resource shrinking... should be false
            shrinkResources false

            //Code shrinking.. should be false
            minifyEnabled false

            //To obfuscate your classes during the build process
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //Build vars
            buildConfigField 'String', 'CONFIG_NAME', '"CONFIG_NAME_VALUE"'
        }
        release {
            debuggable false

            //Resource shrinking...
            shrinkResources true

            //Enable Code shrinking..
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //Build vars
            buildConfigField 'String', 'CONFIG_NAME', '"CONFIG_NAME_VALUE"'

            //Signing..
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // We are using com.android.support.*:23.2.0 instead of 24.0.0-alpha1.
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.pubnub:pubnub-android:3.7.6'
    compile 'com.mixpanel.android:mixpanel-android:4.8.0'
    compile 'com.stripe:stripe-android:1.0.4'
    compile 'io.card:android-sdk:5.3.0'
}

apply plugin: 'com.google.gms.google-services'